javadoc {
    javadocTool = javaToolchains.javadocToolFor {
        // Last version with frames, but it also has search
        languageVersion = JavaLanguageVersion.of(11)
        vendor = JvmVendorSpec.ADOPTIUM
    }

    inputs.file  "src/main/javadoc/overview.html"

    include "com/gtnewhorizon/structurelib/alignment/**"
    include "com/gtnewhorizon/structurelib/constructable/**"
    include "com/gtnewhorizon/structurelib/structure/**"
    include "com/gtnewhorizon/structurelib/util/**"
    include "com/gtnewhorizon/structurelib/StructureLibAPI.java"
    include "com/gtnewhorizon/structurelib/StructureEvent.java"

    classpath += sourceSets.patchedMc.output

    options {
        overview "src/main/javadoc/overview.html"
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://skmedix.github.io/ForgeJavaDocs/javadoc/forge/1.7.10-10.13.4.1614/'
        addBooleanOption("-frames", true)
    }
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
